// Object-Oriented Programming in BanglaCode

// @comment Represents a person with name and age
// @comment Use: dhoro person = notun Manush("name", age);
sreni Manush {
    shuru(naam, boyosh) {
        ei.naam = naam;
        ei.boyosh = boyosh;
    }

    // @comment Prints a self-introduction
    kaj porichoy() {
        dekho("Amar naam", ei.naam, "ebong ami", ei.boyosh, "bochhor boyoshi");
    }

    // @comment Celebrates birthday and increments age
    kaj birthday() {
        ei.boyosh = ei.boyosh + 1;
        dekho(ei.naam, "er ekhon", ei.boyosh, "bochhor");
    }
}

// Create instances
dhoro Ankan = notun Manush("Ankan", 25);
dhoro Saha = notun Manush("Saha", 30);

Ankan.porichoy();
Saha.porichoy();

Ankan.birthday();
Saha.birthday();

// Define a Rectangle class
sreni Rectangle {
    shuru(width, height) {
        ei.width = width;
        ei.height = height;
    }

    kaj area() {
        ferao ei.width * ei.height;
    }

    kaj perimeter() {
        ferao 2 * (ei.width + ei.height);
    }

    kaj display() {
        dekho("Rectangle - Width:", ei.width, "Height:", ei.height);
        dekho("Area:", ei.area());
        dekho("Perimeter:", ei.perimeter());
    }
}

dhoro rect = notun Rectangle(10, 5);
rect.display();

// Define a BankAccount class
sreni BankAccount {
    shuru(accountHolder, balance) {
        ei.accountHolder = accountHolder;
        ei.balance = balance;
    }

    kaj deposit(amount) {
        ei.balance = ei.balance + amount;
        dekho("Deposited", amount, "Taka. New balance:", ei.balance);
    }

    kaj withdraw(amount) {
        jodi (amount > ei.balance) {
            dekho("Insufficient balance!");
            ferao mittha;
        }
        ei.balance = ei.balance - amount;
        dekho("Withdrew", amount, "Taka. Remaining balance:", ei.balance);
        ferao sotti;
    }

    kaj checkBalance() {
        dekho(ei.accountHolder, "er balance:", ei.balance, "Taka");
    }
}

dhoro account = notun BankAccount("Ankan", 1000);
account.checkBalance();
account.deposit(500);
account.withdraw(300);
account.withdraw(2000);
account.checkBalance();
